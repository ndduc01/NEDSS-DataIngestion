buildscript {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
	dependencies {
		classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
	}
}

plugins {
	id 'java'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'gov.cdc.dataingestion.phdc-xsd-jaxb'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}
tasks.withType(Jar).all { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

configurations {
	jaxb
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

task jaxb {
	System.setProperty('javax.xml.accessExternalSchema', 'all')
	def jaxbTargetDir = file("src/main/java")
	doLast {
		jaxbTargetDir.mkdirs()
		ant.taskdef(
				name: 'xjc',
				classname: 'com.sun.tools.xjc.XJCTask',
				classpath: configurations.jaxb.asPath
		)
		ant.jaxbTargetDir = jaxbTargetDir
		ant.xjc(
				destdir: '${jaxbTargetDir}',
				package: 'gov.cdc.dataingestion.nbs.jaxb',
				schema: 'src/main/resources/xsd/PHDC.xsd'
		)
	}
}

compileJava.dependsOn jaxb

dependencies {
	// Jakarta XML Binding API and its runtime implementation
	implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
	implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.2'

	// JAXB implementation for runtime; ensures that JAXB implementation classes are available
	runtimeOnly 'com.sun.xml.bind:jaxb-impl:4.0.0'

	// Required for JAXB code generation if using the Gradle JAXB plugin or similar tools
	jaxb 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
	jaxb 'org.glassfish.jaxb:jaxb-xjc:4.0.2'

	// If using Java 11 or later, these dependencies ensure JAXB functionality is available,
	// as JAXB is not included in the JDK from Java 11 onwards.
	if (JavaVersion.current().isJava11Compatible()) {
		implementation 'javax.xml.bind:jaxb-api:2.3.1'
		implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
		implementation 'com.sun.xml.bind:jaxb-impl:2.3.3'
	}
}

test {
	useJUnitPlatform()
}

jar {
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}
